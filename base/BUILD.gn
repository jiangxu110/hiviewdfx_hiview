# Copyright (c) 2021-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//base/hiviewdfx/hiview/hiview.gni")
import("//build/ohos.gni")

config("hiview_base_config") {
  visibility = [ "*:*" ]

  include_dirs = [
    "$hiview_root/include",
    "event_pool",
    "event_publish/include",
    "event_raw/include",
    "event_store/include",
    "event_threshold/include",
    "include",
    "logstore/include",
    "running_status_logger/include",
    "utility/include",
  ]

  cflags_cc = [ "-D__HIVIEW_OHOS__" ]
  if (is_standard_system) {
    cflags_cc += [ "-D__OHOS_STANDARD_SYSTEM__" ]
  }
}

config("logger_config") {
  visibility = [ "*:*" ]

  include_dirs = [ "include" ]
}

ohos_source_set("logger") {
  public_configs = [ ":logger_config" ]

  sources = [
    "default_logger.cpp",
    "hi_logger.cpp",
    "logger.cpp",
  ]

  external_deps = [ "hilog:libhilog" ]

  part_name = "hiview"
  subsystem_name = "hiviewdfx"
}

ohos_source_set("hiview_base") {
  part_name = "hiview"
  public_configs = [ ":hiview_base_config" ]
  configs = [ "//third_party/jsoncpp:jsoncpp_public_config" ]
  sources = [
    "audit.cpp",
    "audit_log_parser.cpp",
    "dispatch_config.cpp",
    "event.cpp",
    "event_dispatch_queue.cpp",
    "event_loop.cpp",
    "event_source.cpp",
    "hiview_global.cpp",
    "pipeline.cpp",
    "plugin.cpp",
    "plugin_factory.cpp",
    "plugin_proxy.cpp",
    "sys_event.cpp",
  ]

  deps = [
    ":logger",
    "event_pool:eventpool",
    "event_publish:hiview_event_publish",
    "event_raw:hiview_event_raw_base",
    "event_raw:hiview_event_raw_decode",
    "event_raw:hiview_event_raw_encode",
    "event_report:hiview_event_report",
    "event_store:event_store_source",
    "event_threshold:event_threshold_source",
    "logstore:log_store",
    "running_status_logger:running_status_logger_source",
    "utility:hiview_utility",
  ]

  external_deps = [
    "hilog:libhilog",
    "hisysevent:libhisysevent",
  ]
}

ohos_shared_library("hiviewbase") {
  all_dependent_configs = [ ":hiview_base_config" ]
  public_configs = [ ":hiview_base_config" ]
  public_configs += [ "event_report:hiview_event_report_config" ]

  deps = [ ":hiview_base" ]

  version_script = "libhiviewbase.map"

  innerapi_tags = [ "platformsdk" ]
  part_name = "hiview"
  subsystem_name = "hiviewdfx"
  external_deps = [ "hilog:libhilog" ]
}

ohos_static_library("hiviewbase_static_lib_for_tdd") {
  all_dependent_configs = [ ":hiview_base_config" ]
  public_configs = [ ":hiview_base_config" ]
  public_configs += [ "event_report:hiview_event_report_config" ]

  deps = [
    ":hiview_base",
    "logstore:log_store",
  ]

  public_deps = [
    "$hiview_base/event_raw:hiview_event_raw_base",
    "$hiview_base/event_raw:hiview_event_raw_decode",
    "$hiview_base/event_raw:hiview_event_raw_encode",
  ]

  part_name = "hiview"
  subsystem_name = "hiviewdfx"
  external_deps = [ "hilog:libhilog" ]
}
